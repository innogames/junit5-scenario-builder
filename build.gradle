import java.util.regex.Pattern
import org.gradle.util.internal.VersionNumber

plugins {
	id 'java-library'
	id 'me.qoomon.git-versioning' version '5.1.1'
	id 'maven-publish'
	id 'signing'
	id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

group 'com.innogames'
version '0.0.0-SNAPSHOT'

def setVersionFromGit() {
	// Set project version based on git branch or tag using the git-versioning plugin.
	// Version can also be explicitly defined. For example:
	//   gradle -Dgit.ref=$PROVIDED_REF
	//   gradle -Dgit.branch=$PROVIDED_BRANCH_NAME
	//   gradle -Dgit.tag=$PROVIDED_TAG_NAME
	// (See also https://github.com/qoomon/gradle-git-versioning-plugin)
	gitVersioning.apply {
		refs {
			branch('.+') {
				describeTagPattern = Pattern.compile('v(?<version>.*)')
				version = '${describe.tag.version:-0.0.0}-SNAPSHOT'
			}
			tag('v(?<version>.*)') {
				version = '${ref.version}'
			}
		}
	}

	// Use the next minor version if the determined version is a SNAPSHOT version
	VersionNumber current = VersionNumber.parse(project.version)
	if (current.qualifier == 'SNAPSHOT') {
		logger.info('Found SNAPSHOT version. Increase minor version')
		def newVersion = new VersionNumber(current.major, current.minor + 1, 0, current.qualifier)
		project.setVersion(newVersion.toString())
	}

	logger.info("-------------------------------------------")
	logger.info("Current project version: ${project.version}")
	logger.info("-------------------------------------------")
}

setVersionFromGit()

repositories {
	mavenCentral()
}

dependencies {
	def junitVersion = '5.8.1'

	compileOnly "org.junit.jupiter:junit-jupiter-api:$junitVersion"

	testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
	testImplementation "org.mockito:mockito-core:4.0.0"
}

test {
	useJUnitPlatform()
}

java {
	withJavadocJar()
	withSourcesJar()
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java

			pom {
				name = project.name
				description = 'Junit5 extension that introduces a Scenario Builder to your tests'
				url = 'https://github.com/innogames/junit5-scenario-builder'
				licenses {
					license {
						name = 'MIT License'
						url = 'https://opensource.org/licenses/MIT'
					}
				}
				developers {
					developer {
						name = 'Christian Blos'
						email = 'christian.blos@innogames.com'
						organization = 'InnoGames GmbH'
						organizationUrl = 'https://www.innogames.com/'
					}
				}
				scm {
					connection = 'scm:git:git://github.com/innogames/junit5-scenario-builder.git'
					developerConnection = 'scm:git:ssh://github.com:innogames/junit5-scenario-builder.git'
					url = 'https://github.com/innogames/junit5-scenario-builder'
				}
			}
		}
	}

	repositories {
		maven {
			def releasesRepoUrl = layout.buildDirectory.dir('repos/releases')
			def snapshotsRepoUrl = layout.buildDirectory.dir('repos/snapshots')
			url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
		}
	}
}

nexusPublishing {
	repositories {
		sonatype {
			nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
			snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
			username = findProperty('ossrhUsername')
			password = findProperty('ossrhPassword')
		}
	}
}

if (hasProperty('signing.keyId')) {
	signing {
		// Use ascii-armored key when signing.key property is set.
		// If not, signing.secretKeyRingFile property should be present.
		if (hasProperty('signing.key')) {
			useInMemoryPgpKeys(findProperty('signing.keyId'), findProperty('signing.key'), findProperty('signing.password'))
		}
		sign publishing.publications.mavenJava
	}
}

/**
 * Updates the version mentioned in the docs. It will use the determined version based on the
 * current git branch or tag. You can use a specific version via -Dgit.tag=version system prop.
 * E.g. `gradle -Dgit.tag=v0.1.0 updateDocumentedVersion`
 */
tasks.register('updateDocumentedVersion') {
	doFirst {
		File docsFile = file('docs/getting-started.md')
		docsFile.write(docsFile.getText()
			.replaceAll('<version>[0-9a-zA-Z.-]+</version>', "<version>${project.version}</version>")
			.replaceAll('(com.innogames:junit5-scenario-builder:)[0-9a-zA-Z.-]+', "\$1${project.version}"))
	}
}